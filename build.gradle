import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static org.gradle.api.JavaVersion.VERSION_17
import static org.gradle.jvm.toolchain.JavaLanguageVersion.of

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.allopen' apply false
    id 'org.jetbrains.kotlin.plugin.jpa' apply false
    id 'org.jetbrains.kotlin.plugin.noarg' apply false
    id 'io.gitlab.arturbosch.detekt'
    id 'org.jetbrains.dokka'
}

repositories {
    mavenCentral()
}

def javaVersion = VERSION_17

allprojects {

    pluginManager.withPlugin('java') {
        java {
            toolchain {
                languageVersion = of(javaVersion.majorVersion.toInteger())
            }
            withSourcesJar()
        }
    }

    apply plugin: 'org.jetbrains.kotlin.jvm'

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs += [ '-XDenableSunApiLintControl', '-parameters' ]
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            allWarningsAsErrors = true
            languageVersion = '2.1'
            apiVersion = '2.1'
            jvmTarget = javaVersion
            javaParameters = true   // Useful for reflection.
            freeCompilerArgs += [
                    "-java-parameters",
                    "-Xjvm-default=all"
            ]
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform(){
            excludeTags project.hasProperty('runUnstableTests') ? 'runAllTestsNoExclusions' : 'Unstable'
        }

        doFirst {
            systemProperty 'java.io.tmpdir', layout.buildDirectory.get().asFile.absolutePath
        }
    }

    tasks.register('compileAll') { task ->
        description = "Compiles all the Kotlin and Java classes, including all of the test classes."
        group = "verification"

        task.dependsOn tasks.withType(AbstractCompile)
    }

    repositories {
        mavenCentral()
    }

    sourceSets {
        integrationTest {
            kotlin {
                srcDirs += [ 'src/integrationTest/kotlin' ]
            }
            resources {
                srcDirs = [ 'src/integrationTest/resources' ]
            }
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }

    kotlin {
        target {
            java
            compilations.integrationTest {
                associateWith compilations.main
                associateWith compilations.test

                configurations {
                    integrationTestApi.extendsFrom testApi
                    integrationTestImplementation.extendsFrom testImplementation
                    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
                }
            }
        }
    }

    tasks.register('integrationTest', Test) {
        description = "Runs integration tests."
        group = "verification"

        testClassesDirs = project.sourceSets["integrationTest"].output.classesDirs
        classpath = project.sourceSets["integrationTest"].runtimeClasspath

        shouldRunAfter tasks.named('test')
    }

    dependencies {
        // Test utilities
        testImplementation "org.assertj:assertj-core:$assertjVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    }
}

subprojects {
    buildscript {
        configurations.classpath {
            // Force Gradle to use latest dynamic-version plugins.
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'kotlin-allopen'
    apply plugin: 'kotlin-jpa'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    version rootProject.version

    idea{
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    tasks.named("detekt").configure {
        if(file("$projectDir/detekt-baseline.xml").exists()){
            baseline = file("$projectDir/detekt-baseline.xml")
        }
        config.setFrom(files("$rootDir/detekt-config.yml"))
        parallel = true
        reports {
            xml{
                outputLocation.set(file("$projectDir/build/detekt-report.xml"))
            }
            txt.required.set(false)
            sarif.required.set(false)
            html.required.set(false)
        }
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detektPluginVersion"
    }

    tasks.register('allDependencyInsight', DependencyInsightReportTask) {}
    tasks.register('allDependencies', DependencyReportTask) {}

    configurations {
        configureEach {
            resolutionStrategy {
                // FORCE Gradle to use latest dynamic versions.
                cacheDynamicVersionsFor 0, 'seconds'
            }
        }
    }

    // Required for Hibernate
    allOpen {
        annotations(
                "jakarta.persistence.Entity"
        )
    }
}
